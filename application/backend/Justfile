#!/usr/bin/env just --justfile
import "../recipes.justfile"


port := "9100"
host := "0.0.0.0"
enable-coturn := "false"
host-ip := `uv run python -c "import socket; s=socket.socket(socket.AF_INET,socket.SOCK_DGRAM); s.connect(('8.8.8.8',80)); print(s.getsockname()[0]); s.close()"`
stun-server := ""
coturn-port := "443"
dataset-dir := ".data/templates/datasets/coffee-berries"

# Ensure version in GHA workflow matches uv version here
uv_version := "0.9.7"
device := "cpu" # cpu, cu128, xpu

default:
    @just --list

# Generate OpenAPI specification from FastAPI app
gen-openapi: _venv
    PYTHONPATH=app uv run --no-sync openapi.py .build

# Generate OpenAPI specification and convert to markdown
gen-openapi-md: gen-openapi
    uv run --no-sync openapi2markdown .build/openapi-spec.json .build/api-reference.md


# Start FastAPI development server with auto-reload
dev: _venv (download-dataset dataset-dir)
    {{ if enable-coturn == "true" { "COTURN_HOST='" + host-ip + "' COTURN_PORT='" + coturn-port + "'" } else { "" } }} \
    {{ if stun-server != "" { "STUN_SERVER='" + stun-server + "'" } else { "" } }} \
    CORS_ORIGINS='*' \
    WEBRTC_ADVERTISE_IP='{{ host-ip }}' \
    DEVICE='{{ if device =~ "cu.*" { "cuda" } else { device } }}' \
    PYTHONPATH=app uv run uvicorn main:app --reload --host {{ host }} --port {{ port }}


_pre-venv:
    if ! uv self version | grep {{uv_version}} >/dev/null; then \
        curl --proto '=https' --tlsv1.2 -LsSf "https://github.com/astral-sh/uv/releases/download/{{uv_version}}/uv-installer.sh" | sh; \
    fi

_venv: _pre-venv
    uv lock --check
    uv sync --extra {{ device }} --extra mqtt --frozen --trusted-host pypi.org --trusted-host pypi.python.org --trusted-host files.pythonhosted.org

# Generate uv lock file
venv-lock: _pre-venv
    uv lock

# Run unit tests
test-unit: _venv
    if [ -d "./tests/unit" ]; then \
        uv run pytest ./tests/unit -v; \
    else \
        echo "./tests/unit directory not found - skipping unit tests"; \
    fi

# Run integration tests
test-integration: _venv
    if [ -d "./tests/integration" ]; then \
        uv run pytest ./tests/integration -v; \
    else \
        echo "./tests/integration directory not found - skipping integration tests"; \
    fi

# Run all tests (unit + integration)
tests: test-unit test-integration

# Fix code style and format with ruff
style-fix: _venv
    uv run ruff check --config pyproject.toml --fix
    uv run ruff format --config pyproject.toml

# Run linters (ruff + mypy)
lint: _venv _ruff _mypy

_ruff:
    uv run ruff check --config pyproject.toml --output-format=github .
    uv run ruff format --check --config pyproject.toml .

_mypy:
    uv run mypy . --config-file=pyproject.toml

# -------------------------------------------------------------------------------------------------
# Installer
# -------------------------------------------------------------------------------------------------

pyinstaller: _venv (download-dataset dataset-dir)
    echo 'DEVICE="{{device}}"' > .env
    uv sync --extra installer --extra {{device}} --extra mqtt --frozen --trusted-host pypi.org --trusted-host pypi.python.org --trusted-host files.pythonhosted.org
    uv run pyinstaller --clean --noconfirm instant-learn.spec
    mkdir -p dist/instant-learn-backend/InitialData/templates/datasets/coffee-berries
    cp {{ dataset-dir }}/* dist/instant-learn-backend/InitialData/templates/datasets/coffee-berries/
