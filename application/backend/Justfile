#!/usr/bin/env just --justfile
import "../Justfile"

py_path := "tests:app"
# Ensure version in GHA workflow matches uv version here
uv_version := "0.9.7"
ai_extra := "cpu" # cpu, cu126, xpu

default:
    @just --list

# Generate OpenAPI specification from FastAPI app
gen-openapi: _venv
    PYTHONPATH=app uv run --no-sync openapi.py .build

# Generate OpenAPI specification and convert to markdown
gen-openapi-md: gen-openapi
    uv run --no-sync openapi2markdown .build/openapi-spec.json .build/api-reference.md


# Start FastAPI development server with auto-reload
dev: _venv download-dataset
    {{ if enable-coturn == "true" { "ICE_SERVERS='" + coturn-ice-servers + "'" } else if enable-stun == "true" { "ICE_SERVERS='" + stun-ice-servers + "'" } else { "" } }} \
    {{ if webrtc-advertise-ip != "" { "WEBRTC_ADVERTISE_IP='" + webrtc-advertise-ip + "'" } else { "" } }} \
    PYTHONPATH=app uv run uvicorn main:app --reload --host {{ host }} --port {{ container-port }}


_pre-venv:
    if ! uv self version | grep {{uv_version}} >/dev/null; then \
        curl --proto '=https' --tlsv1.2 -LsSf "https://github.com/astral-sh/uv/releases/download/{{uv_version}}/uv-installer.sh" | sh; \
    fi

_venv: _pre-venv
    uv lock --check
    uv sync --extra {{ ai_extra }} --extra mqtt --frozen --trusted-host pypi.org --trusted-host pypi.python.org --trusted-host files.pythonhosted.org

# Generate uv lock file
venv-lock: _pre-venv
    uv lock

# Run unit tests
test-unit: _venv
    if [ -d "./tests/unit" ]; then \
        PYTHONPATH={{py_path}} uv run pytest ./tests/unit -v; \
    else \
        echo "./tests/unit directory not found - skipping unit tests"; \
    fi

# Run integration tests
test-integration: _venv
    if [ -d "./tests/integration" ]; then \
        PYTHONPATH={{py_path}} uv run pytest ./tests/integration -v; \
    else \
        echo "./tests/integration directory not found - skipping integration tests"; \
    fi

# Run all tests (unit + integration)
tests: test-unit test-integration

# Fix code style and format with ruff
style-fix: _venv
    uv run ruff check --config pyproject.toml --fix
    uv run ruff format --config pyproject.toml

# Run linters (ruff + mypy)
lint: _venv _ruff _mypy

_ruff:
    uv run ruff check --config pyproject.toml --output-format=github .
    uv run ruff format --check --config pyproject.toml .

_mypy:
    uv run mypy . --config-file=pyproject.toml

# -------------------------------------------------------------------------------------------------
# Installer
# -------------------------------------------------------------------------------------------------

pyinstaller: _venv
    uv sync --extra installer --extra {{ai_extra}} --frozen --trusted-host pypi.org --trusted-host pypi.python.org --trusted-host files.pythonhosted.org
    uv run pyinstaller --noconfirm geti-prompt.spec