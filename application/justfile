COMPONENT_NAME := "geti-prompt"
DOCKER_IMAGE_DESCRIPTION := COMPONENT_NAME

images-registry := 'localhost:5000/open-edge-platform'
PRODUCT_VERSION := '$(cat `pwd`/../VERSION)'
default_tag := PRODUCT_VERSION + "-" + `git rev-parse --short HEAD`

TAG := env("TAG", default_tag)

docker-build-context := env("DOCKER_BUILD_CONTEXT", "")

registry_build_arg := if env("REGISTRY", "") != "" { " --build-arg REGISTRY=" + env("REGISTRY") } else { "" }
http_proxy_build_arg := if env("http_proxy", "") != "" { " --build-arg http_proxy=" + env("http_proxy") } else { "" }
https_proxy_build_arg := if env("https_proxy", "") != "" { " --build-arg https_proxy=" + env("https_proxy") } else { "" }
no_proxy_build_arg := if env("no_proxy", "") != "" { " --build-arg no_proxy=" + env("no_proxy") } else { "" }
HTTP_PROXY_build_arg := if env("HTTP_PROXY", "") != "" { " --build-arg HTTP_PROXY=" + env("HTTP_PROXY") } else { "" }
HTTPS_PROXY_build_arg := if env("HTTPS_PROXY", "") != "" { " --build-arg HTTPS_PROXY=" + env("HTTPS_PROXY") } else { "" }
NO_PROXY_build_arg := if env("NO_PROXY", "") != "" { " --build-arg NO_PROXY=" + env("NO_PROXY") } else { "" }

###
# docker builder extra arguments
###
docker-extra-args := registry_build_arg + \
    http_proxy_build_arg + \
    https_proxy_build_arg + \
    no_proxy_build_arg + \
    HTTP_PROXY_build_arg + \
    HTTPS_PROXY_build_arg + \
    NO_PROXY_build_arg

###
# Geti Prompt arguments
###
container-port := env("CONTAINER_PORT", "9100")
host-port := env("HOST_PORT", container-port)
workdir-path := env("WORKDIR_PATH", "/geti_prompt")
asset-prefix := env("ASSET_PREFIX", "/html")
data-dir := "/data"
docker-volume := if env("DOCKER_VOLUME", "") != "" { " -v " + env("DOCKER_VOLUME") + ":" + workdir-path + data-dir } else { "" }
static-files-dir := workdir-path + asset-prefix
db-data-dir := workdir-path + data-dir

###
# Justfile targets
###
build-image:
    echo "Building docker image for component: {{COMPONENT_NAME}}"
    docker build \
        {{docker-extra-args}} \
        {{docker-build-context}} \
        --build-arg PUBLIC_API_URL=http://localhost:{{container-port}} \
        --build-arg STATIC_FILES_DIR={{static-files-dir}} \
        --build-arg DB_DATA_DIR={{db-data-dir}} \
        --build-arg ASSET_PREFIX={{asset-prefix}} \
        --build-arg WORKDIR_PATH={{workdir-path}} \
        --label "org.opencontainers.image.description={{DOCKER_IMAGE_DESCRIPTION}}" \
        -t "{{images-registry}}/{{COMPONENT_NAME}}:{{TAG}}" \
        -f docker/Dockerfile .

run-image: build-image
    echo "Running docker image for component: {{COMPONENT_NAME}}"
    docker run --rm \
        --publish {{host-port}}:{{container-port}} \
        --env PORT={{container-port}} \
        {{docker-volume}} \
        --name "{{COMPONENT_NAME}}-{{TAG}}" \
        "{{images-registry}}/{{COMPONENT_NAME}}:{{TAG}}"

show-size:
    @docker image inspect "{{images-registry}}/{{COMPONENT_NAME}}:{{TAG}}" --format='{{{{.Size}}' | numfmt --to=iec
